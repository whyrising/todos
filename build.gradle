import com.github.whyrising.todos.Libs
import com.github.whyrising.todos.Plugins

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath Plugins.Navigation.safeArgs
    }
}

plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.10' apply false
    id 'com.diffplug.spotless' version '6.2.2'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

allprojects {
    group = 'com.github.whyrising.todos'
}

subprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint('0.43.2').userData([max_line_length: "80"])
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors (disabled by default)
            allWarningsAsErrors =
                    project.hasProperty("warningsAsErrors") ? project.warningsAsErrors : false

            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

            jvmTarget = Libs.jvmTarget
        }
    }

    tasks.withType(Test) {
        def decimal = Runtime.getRuntime().availableProcessors() / 2
        maxParallelForks = decimal > 0 ? decimal : 1
        testLogging { events("passed", "skipped", "failed") }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
